#!/usr/bin/env bpftrace
/*
 * syscount.bt	Count system calls.
 *		For Linux, uses bpftrace, eBPF.
 *
 * This is a bpftrace version of the bcc tool of the same name.
 * The bcc versions translates syscall IDs to their names, and this version
 * currently does not. Syscall IDs can be listed by "ausyscall --dump".
 *
 * Copyright 2018 Netflix, Inc.
 * Licensed under the Apache License, Version 2.0 (the "License")
 *
 * 13-Sep-2018	Brendan Gregg	Created this.
 */

BEGIN
{
	printf("Counting syscalls... Hit Ctrl-C to end.\n");
}

tracepoint:syscalls:sys_enter_*
{
	@syscall[probe] = count();
	@process[comm] = count();
}

END
{
    printf("\nTop 50 syscalls IDs:\n");
	print(@syscall);
	clear(@syscall);

	printf("\nTop 50 processes:\n");
	print(@process);
	clear(@process);
}
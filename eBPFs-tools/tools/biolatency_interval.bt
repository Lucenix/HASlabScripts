#!/usr/bin/env bpftrace
/*
 * biolatency.bt	Block I/O latency as a histogram.
 *			For Linux, uses bpftrace, eBPF.
 *
 * This is a bpftrace version of the bcc tool of the same name.
 *
 * Copyright 2018 Netflix, Inc.
 * Licensed under the Apache License, Version 2.0 (the "License")
 *
 * 13-Sep-2018	Brendan Gregg	Created this.
 */

BEGIN
{
	printf("Tracing block device I/O... Hit Ctrl-C to end.\n");
}

tracepoint:block:block_bio_queue
/comm == "python3"/
{
	@start[args.sector] = nsecs;
}

tracepoint:block:block_rq_complete,
tracepoint:block:block_bio_complete
/@start[args.sector]/
{

	if(strcontains(args.rwbs, "R")){
		@read_usecs = hist((nsecs - @start[args.sector]) / 1000);
	} else {
		@write_usecs = hist((nsecs - @start[args.sector]) / 1000);
	}

	delete(@start[args.sector]);
}

interval:s:60
{
	time();
	print(@read_usecs);
	clear(@read_usecs);
	print(@write_usecs);
	clear(@write_usecs);
}

END
{
    time();
    print(@read_usecs);
	clear(@read_usecs);
	print(@write_usecs);
	clear(@write_usecs);
}
